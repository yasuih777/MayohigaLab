{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# ウイルス感染症の数理シミュレーション（応用）: 補足資料\n",
        "\n",
        "Julia言語で書いている"
      ],
      "id": "197d9659-b3e7-47a4-81f9-4ddb5246007a"
    },
    {
      "cell_type": "code",
      "execution_count": 1,
      "metadata": {},
      "outputs": [],
      "source": [
        "using DifferentialEquations\n",
        "using ModelingToolkit\n",
        "using Plots"
      ],
      "id": "1fa68f82-421c-42b7-8552-a0118bb13a15"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# B型感染ウイルスの数理モデリング\n",
        "\n",
        "## 解離・接着までのモデル"
      ],
      "id": "9632359e-38ad-4b6e-9924-660395a6fe07"
    },
    {
      "cell_type": "code",
      "execution_count": 2,
      "metadata": {},
      "outputs": [],
      "source": [
        "let\n",
        "    @independent_variables t\n",
        "    @variables V_dis(t)=1.0 V_att(t)=150.0\n",
        "    @parameters k_dis=3.4 k_att=0.16\n",
        "    \n",
        "    D = Differential(t)\n",
        "    eqs = [\n",
        "        D(V_dis) ~ k_dis * V_att - k_att * V_dis\n",
        "        D(V_att) ~ k_att * V_dis - k_dis * V_att\n",
        "    ]\n",
        "    @mtkbuild sys = ODESystem(eqs, t)\n",
        "    \n",
        "    tspan = (0.0, 30.0)\n",
        "    prob = ODEProblem(sys, [], tspan)\n",
        "    sol = solve(prob)\n",
        "    \n",
        "    dinamics_plot = plot(\n",
        "        sol[t],\n",
        "        [sol[V_dis], sol[V_att]],\n",
        "        title=\"Virus dinamics\",\n",
        "        xlabel=\"Hour\",\n",
        "        ylabel=\"Virus\",\n",
        "        label=[\"dissociate\" \"attach\"],\n",
        "        color=[:blue :orange],\n",
        "    )\n",
        "        \n",
        "    plot(\n",
        "        dinamics_plot,\n",
        "        size=(600, 300),\n",
        "        left_margin = 20Plots.px,\n",
        "        bottom_margin = 20Plots.px,\n",
        "        legend=:outertopright,\n",
        "    )\n",
        "end"
      ],
      "id": "cell-fig-HBV-middle"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## 全状態を考慮したモデル"
      ],
      "id": "bb9bc692-6400-435c-a263-5703f87642b0"
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {},
      "outputs": [
        {
          "output_type": "display_data",
          "metadata": {},
          "data": {
            "text/plain": [
              "virus_dinamics (generic function with 2 methods)"
            ]
          }
        }
      ],
      "source": [
        "function virus_dinamics(\n",
        "    v::Dict{String, Float64},\n",
        "    p::Dict{String, Float64},\n",
        "    t_end::Float64=70.0 / 24.0,\n",
        ")\n",
        "    @independent_variables t\n",
        "    @variables V_dis(t)=v[\"V_dis\"]\n",
        "    @variables V_att(t)=v[\"V_att\"]\n",
        "    @variables V_int(t)=v[\"V_int\"]\n",
        "    @variables V_ccc(t)=v[\"V_ccc\"]\n",
        "    @parameters k_dis=p[\"k_dis\"]\n",
        "    @parameters k_att=p[\"k_att\"]\n",
        "    @parameters k_int=p[\"k_int\"]\n",
        "    @parameters k_ccc=p[\"k_ccc\"]\n",
        "    @parameters k_di=p[\"k_di\"]\n",
        "    @parameters k_dc=p[\"k_dc\"]\n",
        "    @parameters K=p[\"K\"]\n",
        "    \n",
        "    D = Differential(t)\n",
        "    eqs = [\n",
        "        D(V_dis) ~ k_dis * V_att - k_att * V_dis\n",
        "        D(V_att) ~ k_att * V_dis - k_dis * V_att - k_int * V_att\n",
        "        D(V_int) ~ k_int * V_att - k_ccc * (1 - V_ccc / K) * V_int - k_di * V_int\n",
        "        D(V_ccc) ~ k_ccc * (1 - V_ccc / K) * V_int - k_dc * V_ccc\n",
        "    ]\n",
        "    @mtkbuild sys = ODESystem(eqs, t)\n",
        "    \n",
        "    tspan = (0.0, t_end)\n",
        "    prob = ODEProblem(sys, [], tspan)\n",
        "    return solve(prob)\n",
        "end"
      ],
      "id": "18c5b94e-c209-4fff-a8a3-d266b955339f"
    },
    {
      "cell_type": "code",
      "execution_count": 4,
      "metadata": {},
      "outputs": [],
      "source": [
        "let\n",
        "    v::Dict{String, Float64} = Dict(\n",
        "        \"V_dis\" => 1.0,\n",
        "        \"V_att\" => 150.0,\n",
        "        \"V_int\" => 1.0,\n",
        "        \"V_ccc\" => 1.0,\n",
        "    )\n",
        "    p::Dict{String, Float64} = Dict(\n",
        "        \"k_dis\" => 3.4,\n",
        "        \"k_att\" => 0.16,\n",
        "        \"k_int\" => 3.0,\n",
        "        \"k_ccc\" => 0.65,\n",
        "        \"k_di\" => 1.9,\n",
        "        \"k_dc\" => 0.12,\n",
        "        \"K\" => 3.7,\n",
        "    )\n",
        "    sol = virus_dinamics(v, p)\n",
        "    u = reduce(hcat, sol.u)\n",
        "    \n",
        "    dinamics_plot = plot(\n",
        "        sol.t,\n",
        "        [u[1, :] u[2, :] u[3, :] u[4, :]],\n",
        "        title=\"Virus dinamics\",\n",
        "        xlabel=\"Day\",\n",
        "        ylabel=\"Virus\",\n",
        "        label=[\"dissociate\" \"attach\" \"internalization\" \"cccDNA\"],\n",
        "        color=[:blue :orange :red :firebrick],\n",
        "    )\n",
        "    cccDNA_plot = plot(\n",
        "        sol.t,\n",
        "        u[4, :],\n",
        "        title=\"cccDNA dinamics\",\n",
        "        xlabel=\"Day\",\n",
        "        ylabel=\"cccDNA\",\n",
        "        label=\"cccDNA\",\n",
        "        color=:firebrick,\n",
        "    )\n",
        "        \n",
        "    plot(\n",
        "        dinamics_plot,\n",
        "        cccDNA_plot,\n",
        "        size=(900, 300),\n",
        "        left_margin = 20Plots.px,\n",
        "        bottom_margin = 20Plots.px,\n",
        "    )\n",
        "end"
      ],
      "id": "cell-fig-HBV-all"
    },
    {
      "cell_type": "code",
      "execution_count": 5,
      "metadata": {},
      "outputs": [],
      "source": [
        "let\n",
        "    v::Dict{String, Float64} = Dict(\n",
        "        \"V_dis\" => 1.0,\n",
        "        \"V_att\" => 150.0,\n",
        "        \"V_int\" => 1.0,\n",
        "        \"V_ccc\" => 1.0,\n",
        "    )\n",
        "    p::Dict{String, Float64} = Dict(\n",
        "        \"k_dis\" => 3.4,\n",
        "        \"k_att\" => 0.16,\n",
        "        \"k_int\" => 3.0,\n",
        "        \"k_ccc\" => 0.65,\n",
        "        \"k_di\" => 1.9,\n",
        "        \"k_dc\" => 0.12,\n",
        "        \"K\" => 3.7,\n",
        "    )\n",
        "    \n",
        "    inhibition_rate = Vector(0:0.01:1)\n",
        "    \n",
        "    u_att::Vector{Float64} = Vector()\n",
        "    u_int::Vector{Float64} = Vector()\n",
        "    u_ccc::Vector{Float64} = Vector()\n",
        "    \n",
        "    for alpha = inhibition_rate\n",
        "        p[\"k_att\"] = (1 - alpha) * 0.16\n",
        "        sol = virus_dinamics(v, p, 12.0)\n",
        "        u = reduce(hcat, sol.u)\n",
        "        push!(u_att, u[4, end])\n",
        "    end\n",
        "    p[\"k_att\"] = 0.16\n",
        "    \n",
        "    for alpha = inhibition_rate\n",
        "        p[\"k_int\"] = (1 - alpha) * 3.0\n",
        "        sol = virus_dinamics(v, p, 12.0)\n",
        "        u = reduce(hcat, sol.u)\n",
        "        push!(u_int, u[4, end])\n",
        "    end\n",
        "    p[\"k_int\"] = 3.0\n",
        "    \n",
        "    for alpha = inhibition_rate\n",
        "        p[\"k_ccc\"] = (1 - alpha) * 0.65\n",
        "        sol = virus_dinamics(v, p, 12.0)\n",
        "        u = reduce(hcat, sol.u)\n",
        "        push!(u_ccc, u[4, end])\n",
        "    end\n",
        "    p[\"k_ccc\"] = 0.65\n",
        "    \n",
        "    cccDNA_plot = plot(\n",
        "        inhibition_rate,\n",
        "        [u_att u_int u_ccc],\n",
        "        title=\"Inhibition rate effect\",\n",
        "        xlabel=\"Inhibition rate\",\n",
        "        ylabel=\"cccDNA (after 12 days)\",\n",
        "        label=[\"attach\" \"internalization\" \"cccDNA\"],\n",
        "    )\n",
        "        \n",
        "    plot(\n",
        "        cccDNA_plot,\n",
        "        size=(600, 300),\n",
        "        left_margin = 20Plots.px,\n",
        "        bottom_margin = 20Plots.px,\n",
        "        legend=:outertopright,\n",
        "    )\n",
        "end"
      ],
      "id": "cell-fig-HBV-inhibition"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# エンテロウイルスの数理モデリング"
      ],
      "id": "92bf9f54-2f25-4c2e-9928-6614b95b809f"
    },
    {
      "cell_type": "code",
      "execution_count": 6,
      "metadata": {},
      "outputs": [],
      "source": [
        "let\n",
        "    @independent_variables t\n",
        "    @variables T(t)=6.4*10^6 E(t)=1.0*10^-1 I(t)=1.0*10^-1 V(t)=4.5*10^4\n",
        "    @parameters beta=1.0*10^-10 k=0.45 delta=0.55 p=3.3*10^4 c=2.5\n",
        "    \n",
        "    D = Differential(t)\n",
        "    eqs = [\n",
        "        D(T) ~ - beta * T * V\n",
        "        D(E) ~ beta * T * V - k * E\n",
        "        D(I) ~ k * E - delta * I\n",
        "        D(V) ~ p * I - c * V\n",
        "    ]\n",
        "    @mtkbuild sys = ODESystem(eqs, t)\n",
        "    \n",
        "    tspan = (0.0, 30.0)\n",
        "    prob = ODEProblem(sys, [], tspan)\n",
        "    sol = solve(prob)\n",
        "    \n",
        "    cell_plot = plot(\n",
        "        sol[t],\n",
        "        [sol[T], sol[E], sol[I]],\n",
        "        title=\"Cells dinamics\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"Cells\",\n",
        "        label=[\"Target\" \"Eclipse period\" \"Infected\"],\n",
        "        color=[:blue :orange :red],\n",
        "        yscale=:log10,\n",
        "    )\n",
        "        \n",
        "    virus_plot = plot(\n",
        "        sol[t],\n",
        "        sol[V],\n",
        "        title=\"Virus dinamics\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"Virus\",\n",
        "        yscale=:log10,\n",
        "        color=:black,\n",
        "        legend=nothing,\n",
        "    )\n",
        "        \n",
        "    plot(\n",
        "        cell_plot,\n",
        "        virus_plot,\n",
        "        size=(900, 300),\n",
        "        left_margin = 20Plots.px,\n",
        "        bottom_margin = 20Plots.px,\n",
        "    )\n",
        "end"
      ],
      "id": "cell-fig-EV71"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# ヒト免疫不全ウイルスの数理モデリング\n",
        "\n",
        "年齢分布はデルタ関数を仮定した。"
      ],
      "id": "d784bd49-0fb8-4007-83c3-62109f707760"
    },
    {
      "cell_type": "code",
      "execution_count": 7,
      "metadata": {},
      "outputs": [],
      "source": [
        "let\n",
        "    function HIV_delta_model(du, u, h, params, t)\n",
        "        beta, delta, p, c, tau = params\n",
        "        t_tau = h(p, t - tau)[1]\n",
        "        v_tau = h(p, t - tau)[3]\n",
        "        du[1] = - beta * u[1] * u[3]\n",
        "        du[2] = beta * t_tau * v_tau - delta * u[2]\n",
        "        du[3] = p * u[2] - c * u[3]\n",
        "    end\n",
        "    \n",
        "    h(p, t) = ones(3)\n",
        "    beta = 1.0*10^-10;\n",
        "    delta = 0.55;\n",
        "    p = 3.3*10^4;\n",
        "    c = 2.5;\n",
        "    tau = 2.0\n",
        "    lags = [tau]\n",
        "    params = (beta, delta, p, c, tau)\n",
        "\n",
        "    tspan = (0.0, 30.0)\n",
        "    u0 = [6.4*10^6, 1.0*10^-1, 4.5*10^4]\n",
        "    \n",
        "    prob = DDEProblem(\n",
        "        HIV_delta_model, u0, h, tspan, params; constant_lags = lags\n",
        "    )\n",
        "    alg = MethodOfSteps(Tsit5())\n",
        "    sol = solve(prob, alg)\n",
        "    u = reduce(hcat, sol.u)\n",
        "    \n",
        "    cell_plot = plot(\n",
        "        sol.t,\n",
        "        [u[1, :], u[2, :]],\n",
        "        title=\"Cells dinamics\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"Cells\",\n",
        "        label=[\"Target\" \"Infected\"],\n",
        "        color=[:blue :red],\n",
        "        yscale=:log10,\n",
        "    )\n",
        "        \n",
        "    virus_plot = plot(\n",
        "        sol.t,\n",
        "        u[3, :],\n",
        "        title=\"Virus dinamics\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"Virus\",\n",
        "        yscale=:log10,\n",
        "        color=:black,\n",
        "        legend=nothing,\n",
        "    )\n",
        "        \n",
        "    plot(\n",
        "        cell_plot,\n",
        "        virus_plot,\n",
        "        size=(900, 300),\n",
        "        left_margin = 20Plots.px,\n",
        "        bottom_margin = 20Plots.px,\n",
        "    )\n",
        "end"
      ],
      "id": "cell-fig-HIV-delta"
    }
  ],
  "nbformat": 4,
  "nbformat_minor": 5,
  "metadata": {
    "kernelspec": {
      "name": "julia-1.11",
      "display_name": "Julia 1.11.3",
      "language": "julia"
    },
    "language_info": {
      "name": "julia",
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "version": "1.11.3"
    }
  }
}